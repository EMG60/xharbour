#define CRLF Chr(13)+Chr(10)

PROCEDURE Main()

   LOCAL cFolder:=""
   LOCAL cBCC32,cILink32,cTLink32
   LOCAL cAppDir:="",cIni:=""

   IF Len(HB_aParams())=0
      Alert('Use cfgCCompiler.xbs "<BCC_Folder>bin\bcc32.exe" "<xHarbour_Install_folder>\"')
      RETURN
   ENDIF

   cFolder:=HB_aParams()[1]
   cFolder:=Left(cFolder,Len(cFolder)-14)

   IF !File(cFolder+"\bin\bcc32.exe")
      Alert("Can not find "+cFolder+"\bin\bcc32.exe")
      RETURN
   ENDIF

   // BCC32.CFG
   IF File(cFolder+"\bin\bcc32.cfg")
      COPY FILE (cFolder+"\bin\bcc32.cfg") TO (cFolder+"\bin\bcc32.cfg.bak")
   ENDIF
   cBCC32:='-I"'+cFolder+'\include"'+CRLF+;
           '-L"'+cFolder+'\lib;'+cFolder+'\lib\psdk"'+CRLF+;
           '-lj"'+cFolder+'\lib;'+cFolder+'\lib\psdk"'
   MemoWrit(cFolder+"\bin\bcc32.cfg",cBCC32,.F.)


   // ILINK.CFG
   IF File(cFolder+"\bin\ILink32.cfg")
      COPY FILE (cFolder+"\bin\ILink32.cfg") TO (cFolder+"\bin\ILink32.cfg.bak")
   ENDIF
   cILink32:='-L"'+cFolder+'\lib;'+cFolder+'\lib\psdk"'
   MemoWrit(cFolder+"\bin\ILink32.cfg",cILink32,.F.)


   // TLINK.CFG
   IF File(cFolder+"\bin\TLink32.cfg")
      COPY FILE (cFolder+"\bin\TLink32.cfg") TO (cFolder+"\bin\TLink32.cfg.bak")
   ENDIF
   cTLink32:='/L"'+cFolder+'\lib"'
   MemoWrit(cFolder+"\bin\TLink32.cfg",cTLink32,.F.)

   cAppDir:=HB_aParams()[2]

   cIni+="[xHB]"+CRLF
   cIni+="Root      = "+cAppDir+CRLF
   cIni+="LibFolder = "+cAppDir+"lib\"+CRLF
   cIni+="Flags     = -m -n -p -q -gc0"+CRLF
   cIni+="Exe       = harbour.exe"+CRLF
   cIni+=""+CRLF
   cIni+="[BCC]"+CRLF
   cIni+="Root          = "+cFolder+"\"+CRLF
   cIni+="Compile Flags = -O2 -c -5 -6 -a8"+CRLF
   cIni+="Link Flags    = -MAP -FORCE:MULTIPLE"+CRLF

   MemoWrit(cAppDir+"Bin\xbuild.windows.ini",cIni,.F.)


   //Alert(cFolder+"\*.cfg written.")

RETURN


//----------------------------------------------------------------------------------------------------//


FUNCTION HB_aParams()

   LOCAL aArg:={},Arg

   FOR EACH Arg IN WScript:Arguments
      Aadd(aArg,Arg)
   NEXT

RETURN aArg


//----------------------------------------------------------------------------------------------------//


